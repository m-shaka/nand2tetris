// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 512 rows (indexed 0..511, top to bottom)
 * of 256 pixels each (indexed 0..255, left to right). The top left pixel on
 * the screen is indexed (0,0).
 */
class Screen {
    static bool isBlack;
    static int screenBase;
    static Array twoToThe;

    /** Initializes the Screen. */
    function void init() {
        var int i, n;
        let isBlack = true;
        let screenBase = 16384;
        let twoToThe = Array.new(16);
        let i = 0;
        let n = 1;
        while (i < 16) {
            let twoToThe[i] = n;
            let i = i + 1;
            let n = n + n;
        }
        return;

    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int i;
        let i = 0;
        while (i < 8192) {
            do Memory.poke(Memory.peek(screenBase + i), 0);
            let i = i + 1;
        }
        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let isBlack = b;
        return;
    }

    function void updateLocation(int i, int mask) {
        if (isBlack) {
            let screenBase[i] = screenBase[i] | mask;
        } else {
            let screenBase[i] = screenBase[i] & ~mask;
        }
        return;
    }
    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int quot, bit, address;
        let quot = x / 16;
        let bit = x - (quot * 16);
        let address = (y * 32) + quot;
        do Screen.updateLocation(address, twoToThe[bit]);
        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int dx, dy, a, b, diff, aSign, bSign;
        let a = 0;
        let b = 0;
        let aSign = 1;
        let bSign = 1;
        let dx = x2 - x1;
        let dy = y2 - y1;
        if (dx < 0) {
            let aSign = -1;
        }
        if (dy < 0) {
            let bSign = -1;
        }
        let dx = Math.abs(dx);
        let dy = Math.abs(dy);
        let diff = 0;
        if ((dx = 0) & (dy = 0)) {
            do Screen.drawPixel(x1, y1);
            return;
        }
        while (
            (~(a > dx) | (dx = 0)) &
            (~(b > dy) | (dy = 0))
        ) {
            do Screen.drawPixel(x1 + (aSign * a), y1 + (bSign * b));
            if (diff < 0) {
                let a = a + 1;
                let diff = diff + dy;
            } else {
                let b = b + 1;
                let diff = diff - dx;
            }
        }
        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int i, dy, tmp;
        let dy = y2 - y1;
        if (dy < 0) {
            let tmp = y2;
            let y2 = y1;
            let y1 = tmp;
        }
        if ((x2 - x1) < 0) {
            let tmp = x2;
            let x2 = x1;
            let x1 = tmp;
        }
        let i = 0;
        let dy = Math.abs(dy);
        while (~(i > dy)) {
            do Screen.drawHorizontal(x1, y1 + i, x2);
            let i = i + 1;
        }
        return;
    }

    function void drawHorizontal(int x1, int y1, int x2) {
        var int maskStart, maskEnd, indexStart, indexEnd, indexRow;
        let indexStart = x1 / 16;
        let indexEnd = x2 / 16;
        let maskStart = ~(twoToThe[x1 - (indexStart * 16)] - 1);
        let indexRow = y1 * 32;
        let maskEnd =twoToThe[x2 - (indexEnd * 16) + 1] - 1;

        if (indexStart = indexEnd) {
            do Screen.updateLocation(indexStart + indexRow, maskStart & maskEnd);
            return;
        }

        do Screen.updateLocation(indexStart + indexRow, maskStart);
        let indexStart = indexStart + 1;
        while (indexStart < indexEnd) {
            do Screen.updateLocation(indexStart + indexRow, -1);
            let indexStart = indexStart + 1;
        }
        do Screen.updateLocation(indexEnd + indexRow, maskEnd);
        return;
    }
    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int dy, tempR, r2;
        if (r > 181) {
            return;
        }
        let dy = -r;
        let r2 = r * r;
        while (dy < (r + 1)) {
            let tempR = Math.sqrt(r2 - (dy * dy));
            do Screen.drawHorizontal(x - tempR, y + dy, x + tempR);
            let dy = dy + 1;
        }
        return;
    }
}
